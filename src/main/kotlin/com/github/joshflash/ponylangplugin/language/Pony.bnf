{
  parserClass="com.github.joshflash.ponylangplugin.parser.PonyParser"
  parserUtilClass="com.github.joshflash.ponylangplugin.parser.PonyParserUtil"
  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Pony"
  psiImplClassSuffix="Impl"
  psiPackage="com.github.joshflash.ponylangplugin.language.psi"
  psiImplPackage="com.github.joshflash.ponylangplugin.language.psi.impl"
  psiImplUtilClass="com.github.joshflash.ponylangplugin.language.psi.impl.PonyPsiImplUtil"

  elementTypeHolderClass="com.github.joshflash.ponylangplugin.language.psi.PonyTypes"
  elementTypeClass="com.github.joshflash.ponylangplugin.language.psi.PonyElementType"
  tokenTypeClass="com.github.joshflash.ponylangplugin.language.psi.PonyTokenType"

  tokens = [
  // keywords
    USE="use"
    TYPEDEF="type"
    INTERFACE="interface"
    TRAIT="trait"
    PRIMITIVE="primitive"
    STRUCT="struct"
    CLASS="class"
    ACTOR="actor"
    OBJECT="object"
    FUN="fun"
    BE="be"
    NEW="new"
    LET="let"
    VAR="var"
    EMBED="embed"
    THIS="this"
    IS="is"
    AS="as"
    ISNT="isnt"
    IF="if"
    ELSEIF="elseif"
    THEN="then"
    TRY="try"
    ELSE="else"
    END="end"
    FOR="for"
    IN="in"
    DO="do"
    WHERE="where"
    WHILE="while"
    WITH="with"
    MATCH="match"
    RECOVER="recover"
    CONSUME="consume"
    IFDEF="ifdef"
    IFTYPE="iftype"
    REPEAT="repeat"
    UNTIL="until"
    ISO="iso"
    REF="ref"
    TRN="trn"
    VAL="val"
    BOX="box"
    TAG="tag"
    RETURN="return"
    BREAK="break"
    CONTINUE="continue"
    ERROR="error"
    COMP_INT="compile_intrinsic"
    COMP_ERR="compile_error"
    SOURCELOC="__loc"
    ADDRESSOF="addressof"
    DIGESTOF="digestof"
  // symbols
    EQUALS="="
    ARROW="=>"
    TYPE_ARROW="->"
    COLON=":"
    SEMI=";"
    COMMA=","
    DOT="."
    LP="("
    RP=")"
    LBK="["
    RBK="]"
    LBC="{"
    RBC="}"
    RCVR="@{"
    AT="@"
    QM="?"
    CARET="^"
    BANG="!"
    CHAIN=".>"
    TILDE="~"
    PIPE="|"
    AMP="&"
    HASH="#"
    ELIPSIS="..."
  // operators
    ISA="<:"
    AND="and"
    OR="or"
    NOT="not"
    XOR="xor"
    ADD="+"
    SUB="-"
    MUL="*"
    DIV="/"
    REM="%"
    MOD="%%"
    SHL="<<"
    SHR=">>"
    EQ="=="
    NEQ="!="
    LT="<"
    GT=">"
    LEQ="<="
    GEQ=">="
    ADD_UNSAFE="+~"
    SUB_UNSAFE="-~"
    MUL_UNSAFE="*~"
    DIV_UNSAFE="/~"
    REM_UNSAFE="%~"
    MOD_UNSAFE="%%~"
    SHL_UNSAFE="<<~"
    SHR_UNSAFE=">>~"
    EQ_UNSAFE="==~"
    NEQ_UNSAFE="!=~"
    LT_UNSAFE="<~"
    GT_UNSAFE=">~"
    LEQ_UNSAFE="<=~"
    GEQ_UNSAFE=">=~"
    ADD_PARTIAL="+?"
    SUB_PARTIAL="-?"
    MUL_PARTIAL="*?"
    DIV_PARTIAL="/?"
    REM_PARTIAL="%?"
    MOD_PARTIAL="%%?"
  // lexemes
    true="true"
    false="false"
    generic_id="regexp:[A-Z]"
    type_id="regexp:[A-Z][a-zA-Z0-9_]*"
    id="regexp:[a-z_][a-zA-Z0-9_]*"
    digit="regexp:[0-9]+"
    string="regexp:(\"(\\[abefnrtv0\"\\]|[^\"\\])*\")"
    gencap="regexp:#(read|send|share|alias|any)"
    line_comment="regexp://.*"
    block_comment="regexp:/\*([^*]|\*+[^*/])*(\*+/)?"
  ]
}

module ::= [string] usestmt* class_def* ;

usestmt ::= USE [id EQUALS] (string | use_ffi) [IF infix] ;

use_ffi ::= AT (id | string) typeargs LP [params] RP [QM] ;

class_def ::= declaration [annotatedids] [AT] [cap] type_ref [typeparams] [IS type] [string] members ;

type_ref ::= type_id

generic_type ::= generic_id [CARET | BANG]

declaration ::= (TYPEDEF | INTERFACE | TRAIT | PRIMITIVE | STRUCT | CLASS | ACTOR) ;

members ::= field* method* ;

field ::= binding id COLON type [EQUALS infix] [string] ;

binding ::= (VAR | LET | EMBED)

method ::= (FUN | BE | NEW) [annotatedids] [cap | AT] id [typeparams] LP [params] RP [COLON type] [QM] [string] [ARROW rawseq] ;

annotatedrawseq ::= [annotatedids] (exprseq | jump) ;

rawseq ::= exprseq | jump ;

exprseq ::= assignment [nosemi] ;

nosemi ::= jump | nextexprseq [semiexpr | jump ] ;

nextexprseq ::= nextassignment+ ;

semiexpr ::= SEMI (exprseq | jump) ;

jump ::= (RETURN | BREAK | CONTINUE | ERROR | COMP_INT | COMP_ERR) [rawseq] ;

nextassignment ::= nextinfix [EQUALS assignment] ;

assignment ::= infix [EQUALS assignment] ;

nextinfix ::= nextterm infixop*;

infix ::= term infixop* ;

infixop ::= binop | isop | AS type

isop ::= (IS | ISNT) term ;

safeop ::=
    AND | OR  | XOR | ADD | SUB | MUL
  | DIV | REM | MOD | SHL | SHR | EQ
  | NEQ | LT  | LEQ | GEQ | GT

unsafeop ::=
    ADD_UNSAFE | SUB_UNSAFE | MUL_UNSAFE
  | DIV_UNSAFE | REM_UNSAFE | MOD_UNSAFE
  | SHL_UNSAFE | SHR_UNSAFE | EQ_UNSAFE
  | NEQ_UNSAFE | LT_UNSAFE  | LEQ_UNSAFE
  | GT_UNSAFE  | GEQ_UNSAFE

partialop ::=
    ADD_PARTIAL | SUB_PARTIAL | MUL_PARTIAL
  | DIV_PARTIAL | REM_PARTIAL | MOD_PARTIAL

binop ::=
   (safeop | unsafeop | partialop) [QM] term ;

nextterm ::=
    construct
  | nextpattern ;

term ::=
    construct
  | pattern ;

construct ::=
    ifblock
  | whileloop
  | forloop
  | IFDEF [annotatedids] infix THEN rawseq [elifdef | (ELSE annotatedrawseq)] END
  | IFTYPE [annotatedids] istype [eliftype | (ELSE annotatedrawseq)] END
  | MATCH [annotatedids] rawseq caseexpr* [ELSE annotatedrawseq] END
  | REPEAT [annotatedids] rawseq UNTIL annotatedrawseq [ELSE annotatedrawseq] END
  | WITH [annotatedids] withelem (COMMA withelem)* DO rawseq END
  | TRY [annotatedids] rawseq [ELSE annotatedrawseq] [THEN annotatedrawseq] END
  | RECOVER [annotatedids] [cap] rawseq END
  | CONSUME [cap] term
  | HASH postfix ;

withelem ::= idseq EQUALS rawseq ;

caseexpr ::=
    PIPE [annotatedids] [casepattern] [IF rawseq] [ARROW rawseq] ;

eliftype ::=
    ELSEIF [annotatedids] istype [eliftype | (ELSE annotatedrawseq)] ;

istype ::= type ISA type THEN rawseq ;

elifdef ::=
    ELSEIF [annotatedids] infix THEN rawseq [elifdef | (ELSE annotatedrawseq)] ;

elif ::=
    ELSEIF [annotatedids] rawseq THEN rawseq [elif | (ELSE annotatedrawseq)] ;

idseq ::=
    id
  | LP idseq_in_seq (COMMA idseq_in_seq)* RP ;

idseq_in_seq ::=
    id
  | LP idseq_in_seq (COMMA idseq_in_seq)* RP ;

nextpattern ::=
    binding id [COLON type]
  | nextparampattern ;

casepattern ::=
    binding id [COLON type]
  | caseparampattern ;

pattern ::=
    binding id [COLON type]
  | parampattern ;

nextparampattern ::=
    (NOT | ADDRESSOF | "\n" SUB | newline SUB_UNSAFE | DIGESTOF) parampattern
  | nextpostfix ;

caseparampattern ::=
    (NOT | ADDRESSOF | ["\n"] SUB | [newline] SUB_UNSAFE | DIGESTOF) caseparampattern
  | casepostfix ;

parampattern ::=
    (NOT | ADDRESSOF | ["\n"] SUB | [newline] SUB_UNSAFE | DIGESTOF) parampattern
  | postfix ;

nextpostfix ::= nextatom postfixelem* ;

casepostfix ::= caseatom postfixelem* ;

postfix ::= atom postfixelem* ;

postfixelem ::= dot_id | tilde_id | chain_id | typeargs | call ;

call ::= LP [positional] [named] RP [QM] ;

chain_id ::= CHAIN id ;

tilde_id ::= TILDE id ;

dot_id ::= DOT id ;

nextatom ::=
    particle
  | ifblock
  | whileloop
  | forloop
  | "\n" LP rawseq [tuple] RP
  | "\n" LBK [AS type COLON] [rawseq] RBK ;

caseatom ::=
    particle
  | whileloop
  | forloop
  | LP rawseq [tuple] RP
  | LBK [AS type COLON] [rawseq] RBK ;

atom ::=
    particle
  | ifblock
  | whileloop
  | forloop
  | LP rawseq [tuple] RP
  | LBK [AS type COLON] [rawseq] RBK ;

particle ::=
    id ["'"]
  | type_ref
  | THIS
  | literal
  | OBJECT [annotatedids] [cap] [IS type] members END
  | LBC [annotatedids] [cap] [id] [typeparams] LP [lambdaparams] RP [lambdacaptures] [COLON type] [QM] ARROW rawseq RBC [cap]
  | RCVR [annotatedids] [cap] [id] [typeparams] LP [lambdaparams] RP [lambdacaptures] [COLON type] [QM] ARROW rawseq RBC [cap]
  | AT (id | string) [typeargs] LP [positional] [named] RP [QM]
  | SOURCELOC ;

whileloop ::= WHILE [annotatedids] rawseq DO rawseq [ELSE annotatedrawseq] END ;

forloop ::= FOR [annotatedids] idseq IN rawseq DO rawseq [ELSE annotatedrawseq] END ;

ifblock ::= IF [annotatedids] rawseq THEN rawseq [elif | (ELSE annotatedrawseq)] END ;

tuple ::= COMMA rawseq (COMMA rawseq)* ;

lambdacaptures ::= LP (lambdacapture | THIS) (COMMA (lambdacapture | THIS))* RP ;

lambdacapture ::= id [COLON type] [EQUALS infix] ;

lambdaparams ::= lambdaparam (COMMA lambdaparam)* ;

lambdaparam ::= id [COLON type] [EQUALS infix] ;

positional ::= rawseq (COMMA rawseq)* ;

named ::= WHERE namedarg (COMMA namedarg)* ;

namedarg ::= id EQUALS rawseq ;

type ::= atomtype [TYPE_ARROW type] ;

atomtype ::=
    THIS
  | generic_type
  | cap
  | LP infixtype [tupletype] RP
  | nominal
  | lambdatype
  | barelambdatype ;

barelambdatype ::=
    RCVR [cap] [id] [typeparams] LP [type (COMMA type)*] RP [COLON type] [QM] RBC [cap | gencap] [CARET | BANG] ;

lambdatype ::=
    LBC [cap] [id] [typeparams] LP [type (COMMA type)*] RP [COLON type] [QM] RBC [cap | gencap] [CARET | BANG] ;

tupletype ::= COMMA infixtype (COMMA infixtype)* ;

infixtype ::= type (uniontype | isecttype)* ;

isecttype ::= AMP type ;

uniontype ::= PIPE type ;

nominal ::= [id DOT] type_ref [typeargs] [cap | gencap] [CARET | BANG] ;

cap ::= ISO | REF | TRN | VAL | BOX | TAG ;

typeargs ::= LBK typearg (COMMA typearg)* RBK ;

typeparams ::= LBK typeparam (COMMA typeparam)* RBK ;

params ::= (param | ELIPSIS) (COMMA (param | ELIPSIS))* ;

typeparam ::= (type_ref | generic_type) [COLON type] [EQUALS typearg] ;

typearg ::=
    type
  | literal
  | HASH postfix ;

literal ::=
    true
  | false
  | int
  | float
  | string ;

param ::= id ["'"] COLON type [EQUALS infix] ;

annotatedids ::= "\\" id (COMMA id)* "\\" ;

int ::=
    digit         (digit    | "_")*
  | "0x" hexdigit (hexdigit | "_")*
  | "0b" bindigit (bindigit | "_")* ;

float ::= digit (digit | "_")* [DOT digit (digit | "_")*] [exponent] ;

exponent ::= ("e" | "E") [ADD | SUB] digit (digit | "_")* ;

hexdigit ::= digit | hexletter ;

hexletter ::=
    "A" | "B" | "C" | "D" | "E" | "F"
    "a" | "b" | "c" | "d" | "e" | "f" ;

bindigit ::= "0" | "1" ;

comment ::= line_comment | block_comment